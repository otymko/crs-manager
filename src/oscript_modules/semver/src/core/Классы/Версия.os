#Использовать strings
#Использовать logos

Перем Лог;

Перем Основная Экспорт; // Число
Перем Второстепенная Экспорт; // Число
Перем Патч Экспорт; // число

Перем ПреРелиз Экспорт; // Массив
Перем НомерБилда Экспорт; // Массив;
Перем ОшибкаЧтенияВерсии Экспорт;

Процедура ПриСозданииОбъекта(Знач ВерсияСтрокой)

	Основная = 0;
	Второстепенная = 0;
	Патч = 0;
	ПреРелиз = Новый Массив;

	Прочитать(ВерсияСтрокой);

КонецПроцедуры

// Возвращает наличие ошибки при чтении версии
//
// Возвращаемое значение:
//   булево - истина / ложь
Функция Ошибка() Экспорт
	
	Возврат Не ПустаяСтрока(ОшибкаЧтенияВерсии);

КонецФункции

// Возвращает описание ошибки при чтении версии
//
// Возвращаемое значение:
//   строка - подробное описание чтения версии из строки
Функция ПолучитьОписаниеОшибки() Экспорт
	Возврат ОшибкаЧтенияВерсии;
КонецФункции

// Возвращает строковое представление версии
//
// Возвращаемое значение:
//   строка - строковое представление версии (типа: 1.0.0, 2.0.0)
Функция ВСтроку() Экспорт
	
	СтрокаВерсии = СтрШаблон("%1.%2.%3", Строка(Основная), Строка(Второстепенная), Строка(Патч));
	
	Возврат СтрокаВерсии;
	
КонецФункции

// Выполняет сравнение версии с входящей версией
//
// Параметры:
//   ВходящаяВерсия - Класс Версия - версия для сравнения
// Возвращаемое значение:
//   Число - результат сравнения в числе (0 = Равны, -1 = Меньше, 1 = Больше), относительно входящей версии
Функция Сравнить(Знач ВходящаяВерсия) Экспорт
	
	Лог.Отладка("Класс входящей версии %1", ВходящаяВерсия);
	
	Если НЕ Основная = ВходящаяВерсия.Основная Тогда
		
		Возврат ?(Основная > ВходящаяВерсия.Основная,1,-1);

	КонецЕсли;

	Если НЕ Второстепенная = ВходящаяВерсия.Второстепенная Тогда
		
		Возврат ?(Второстепенная > ВходящаяВерсия.Второстепенная,1,-1);

	КонецЕсли;

	Если НЕ Патч = ВходящаяВерсия.Патч Тогда
		
		Возврат ?(Патч > ВходящаяВерсия.Патч,1,-1);

	КонецЕсли;

	Если ПреРелиз.Количество() = 0 
		И ВходящаяВерсия.ПреРелиз.Количество() = 0 Тогда
		Возврат 0;
	КонецЕсли;

	Если ПреРелиз.Количество() = 0 
		И ВходящаяВерсия.ПреРелиз.Количество() = 0 Тогда
		Возврат 0;
	КонецЕсли;

	Если ПреРелиз.Количество() = 0 
		И ВходящаяВерсия.ПреРелиз.Количество() > 1 Тогда
		Возврат 1;
	КонецЕсли;

	Если ПреРелиз.Количество() > 0 
		И ВходящаяВерсия.ПреРелиз.Количество() = 0 Тогда
		Возврат -1;
	КонецЕсли;

	Возврат 0;
	
КонецФункции

// Проверяет равенство версии с входящей версией
//
// Параметры:
//   ВходящаяВерсия - Класс Версия - версия для сравнения
// Возвращаемое значение:
//   Булево - истина / ложь
Функция Равны(Знач ВходящаяВерсия) Экспорт
	Возврат Сравнить(ВходящаяВерсия) = 0;
КонецФункции

// Проверяет не равенство версии с входящей версией
//
// Параметры:
//   ВходящаяВерсия - Класс Версия - версия для сравнения
// Возвращаемое значение:
//   Булево - истина / ложь
Функция НеРавны(Знач ВходящаяВерсия) Экспорт
	Возврат НЕ Сравнить(ВходящаяВерсия) = 0;
КонецФункции

// Проверяет, что текущая версия меньше входящей версии
//
// Параметры:
//   ВходящаяВерсия - Класс Версия - версия для сравнения
// Возвращаемое значение:
//   Булево - истина / ложь
Функция Меньше(Знач ВходящаяВерсия) Экспорт
	Возврат Сравнить(ВходящаяВерсия) = -1;
КонецФункции

// Проверяет, что текущая версия меньше или равна входящей версии
//
// Параметры:
//   ВходящаяВерсия - Класс Версия - версия для сравнения
// Возвращаемое значение:
//   Булево - истина / ложь
Функция МеньшеИлиРавны(Знач ВходящаяВерсия) Экспорт
	Возврат Сравнить(ВходящаяВерсия) <= 0;
КонецФункции

// Проверяет, что текущая версия больше входящей версии
//
// Параметры:
//   ВходящаяВерсия - Класс Версия - версия для сравнения
// Возвращаемое значение:
//   Булево - истина / ложь
Функция Больше(Знач ВходящаяВерсия) Экспорт
	Возврат Сравнить(ВходящаяВерсия) = 1;
КонецФункции

// Проверяет, что текущая версия больше или равна входящей версии
//
// Параметры:
//   ВходящаяВерсия - Класс Версия - версия для сравнения
// Возвращаемое значение:
//   Булево - истина / ложь
Функция БольшеИлиРавны(Знач ВходящаяВерсия) Экспорт
	Возврат Сравнить(ВходящаяВерсия) >= 0;
КонецФункции

Процедура Прочитать(Знач ВерсияСтрокой)
	
	Если СтрДлина(ВерсияСтрокой) = 0 Тогда
		ОшибкаЧтенияВерсии = "Длина строки версии 0";
		Возврат; 
	КонецЕсли;

	ВерсияСтрокой = ПодготовитьКЧтению(ВерсияСтрокой);

	МассивСтрокВерсии = СтрРазделить(ВерсияСтрокой, ".");
	
	Если МассивСтрокВерсии.Количество() < 3 Тогда
		ОшибкаЧтенияВерсии = "Не найдены все составляющие версии";
		Возврат; 
	КонецЕсли;

	СтрокаОсновнаяВерсия = МассивСтрокВерсии[0];

	Если Не СтроковыеФункции.ТолькоЦифрыВСтроке(СтрокаОсновнаяВерсия) Тогда
	
		ОшибкаЧтенияВерсии = СтрШаблон("Основная версия <%1> должна содержать только цифры",СтрокаОсновнаяВерсия);
		Возврат; 
	
	КонецЕсли;

	Основная = Число(СтрокаОсновнаяВерсия);
	
	СтрокаВторостепеннаяВерсия = МассивСтрокВерсии[1];


	Если Не СтроковыеФункции.ТолькоЦифрыВСтроке(СтрокаВторостепеннаяВерсия) Тогда
	
		ОшибкаЧтенияВерсии = СтрШаблон("Второстепенная версия <%1> должна содержать только цифры",СтрокаОсновнаяВерсия);
		Возврат; 
	
	КонецЕсли;

	Второстепенная = Число(СтрокаВторостепеннаяВерсия);

	СтрокаПатчаВерсии = МассивСтрокВерсии[2];

	Если Не СтроковыеФункции.ТолькоЦифрыВСтроке(СтрокаПатчаВерсии) Тогда
	
		ОшибкаЧтенияВерсии = СтрШаблон("Версия патча <%1> должна содержать только цифры",СтрокаОсновнаяВерсия);
		Возврат; 
	
	КонецЕсли;

	Патч = Число(СтрокаПатчаВерсии);

КонецПроцедуры

Функция ПодготовитьКЧтению(Знач СтрокаВерсии)
	
	Если СтрНачинаетсяС(СтрокаВерсии, "v") Тогда
		СтрокаВерсии = СтрЗаменить(СтрокаВерсии, "v", "");
	КонецЕсли;

	МассивСтрокВерсии = СтрРазделить(СтрокаВерсии, ".");

	КоличествоДобавления = 3 - МассивСтрокВерсии.Количество();

	Для ИИ = 1 ПО КоличествоДобавления Цикл
		МассивСтрокВерсии.Добавить(0);
	КонецЦикла;

	Возврат СтрСоединить(МассивСтрокВерсии, ".");

КонецФункции

Лог = Логирование.ПолучитьЛог("oscript.lib.semver.version");
